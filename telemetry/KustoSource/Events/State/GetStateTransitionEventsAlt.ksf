#include("..\Raw\ScopedRawEvents_7Days.ksf")
// When "environmentstatemanager_setenvironmentstateasync" are un-reliable, this can be used to determine state transtions.
let GetStateTransitionEventsAlt = (fromState: string, toState: string) { 
    let ManagerEvents = ScopedRawEvents
        | where msg == "environment_manager_update_complete"
        | project CloudEnvironmentState, ['time'], CorrelationId, WorkspaceId, HttpRequestMethod, HttpRequestUri
        | summarize arg_min(['time'], *) by CloudEnvironmentState, WorkspaceId
        | order by ['time'] asc
        | extend Row = row_number(1);
    let MaterializedManagerEvents = materialize(ManagerEvents);
    MaterializedManagerEvents
        | extend TargetJoin = Row + 1
        | join (MaterializedManagerEvents) on $left.TargetJoin == $right.Row
        | extend CloudEnvironmentOldState = CloudEnvironmentState
        | extend CloudEnvironmentNewState = CloudEnvironmentState1
        | project CloudEnvironmentOldState, CloudEnvironmentNewState, WorkspaceId = WorkspaceId1, ['time'] = time1, HttpRequestMethod = HttpRequestMethod1, HttpRequestUri = HttpRequestUri1, CorrelationId = CorrelationId1
        | where CloudEnvironmentOldState == fromState and CloudEnvironmentNewState == toState
};