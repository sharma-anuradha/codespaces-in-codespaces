#function("Codespace Creation - Success and NonSuccess per hour includes infra and user failures.")
(T:(EventAtCreateRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, CreateOutcome: string, EventAtProvisioningToAvailable_Time: datetime, EventAtCreateRequestStart_Time: datetime, EventAtSelectedCustomContainer: long))
{
    T
    | where TimeStamp < ago(1h)
    | where EventAtCreateRequestStop_ResponseCode != 307
    | extend SuccessResult = iff(CreateOutcome == "Available" and ResponseOutcome == "Success", 1, 0)
    | extend TotalRequests = 1
    | make-series Success_ = sum(SuccessResult), Total_ = sum(TotalRequests) 
        on TimeStamp step 1h
    | mv-expand Success_, Total_, TimeStamp
    | project Success = toint(Success_), Total = toint(Total_), Time = todatetime(TimeStamp)
    | extend NonSuccess = Total - Success
    | project Time, Success, NonSuccess
}