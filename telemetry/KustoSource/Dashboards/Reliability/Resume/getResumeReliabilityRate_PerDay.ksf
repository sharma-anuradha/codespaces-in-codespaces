#function("Computes reliability rate of resume per day.")
(T:(EventAtResumeRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, ResumeOutcome: string))
{
    T
    | where EventAtResumeRequestStop_ResponseCode != 307
    | where TimeStamp < ago(1h)
    | make-series 
            Total_ = count(), 
            Failed_ =  countif(ResponseOutcome startswith "Infrastructure:" or 
                ResponseOutcome startswith "Bug:" or 
                ResumeOutcome startswith "Infrastructure:" or 
                ResumeOutcome startswith "Bug:") 
            on TimeStamp step 1d
    | mv-expand Total_, Failed_, TimeStamp
    | project Total = toint(Total_), Failed = toint(Failed_), Time = todatetime(TimeStamp)
    | extend Success = Total - Failed
    | extend  Rate = (Success * 100.0) / Total
    | project Time, Rate
}