#function("Tabulate resume time durations from request start to enviornment available.")
(T:(EventAtResumeRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, ResumeOutcome: string, EventAtResumeRequestStart_Time: datetime, EventAtResumeRequestStop_Time: datetime))
{
    T
    | where TimeStamp < ago(1h)
    | where EventAtResumeRequestStop_ResponseCode != 307
    | where ResumeOutcome == "Available" and ResponseOutcome == "Success"
    | extend DurationInSeconds = (EventAtResumeRequestStop_Time - EventAtResumeRequestStart_Time) / 1s
    | make-series percentile(DurationInSeconds, 50), percentile(DurationInSeconds, 90), percentile(DurationInSeconds, 99) on TimeStamp step 1h
    | mv-expand percentile_DurationInSeconds_50, percentile_DurationInSeconds_90, percentile_DurationInSeconds_99, TimeStamp
    | project 
        Time = todatetime(TimeStamp),
        Percentile50 = todouble(percentile_DurationInSeconds_50), 
        Percentile90 = todouble(percentile_DurationInSeconds_90), 
        Percentile99 = todouble(percentile_DurationInSeconds_99)
}