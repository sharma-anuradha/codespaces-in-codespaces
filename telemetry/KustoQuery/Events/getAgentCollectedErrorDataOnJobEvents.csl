.create-or-alter function
with (docstring = 'Gets Error received from agent events.', folder='Events')
getAgentCollectedErrorDataOnJobEvents
(T:(msg: string,
    CorrelationId: string,
    JobCollectedData: string))
{
    T
        | where msg == "start_environment_handler_process_complete"
        | project CorrelationId, JobCollectedData
        | where JobCollectedData != ""
        | extend ParsedJobCollectedData = parse_json(JobCollectedData)
        | extend JobErrors = ParsedJobCollectedData.errors
        | where JobErrors != "[]"
        | extend EventAtAgentCollectedError_JobError = tostring(JobErrors[0])
        | extend EventAtAgentCollectedError_CodespaceId = tostring(ParsedJobCollectedData.environmentId)
        | extend EventAtAgentCollectedError_ErrorType = tostring(ParsedJobCollectedData.jobErrorType)
        | extend EventAtAgentCollectedError = 1
        | extend EventAtAgentCollectedError_CorrelationId = CorrelationId
        | project EventAtAgentCollectedError_CorrelationId, 
            EventAtAgentCollectedError_JobError, 
            EventAtAgentCollectedError, 
            EventAtAgentCollectedError_CodespaceId,
            EventAtAgentCollectedError_ErrorType
}
