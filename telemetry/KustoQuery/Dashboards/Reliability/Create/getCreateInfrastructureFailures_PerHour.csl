.create-or-alter function
with (docstring = 'Counts the total infrastructure failures or bugs for create per hour.', folder='Dashboards/Reliability/Create')
getCreateInfrastructureFailures_PerHour
(T:(EventAtCreateRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, CreateOutcome: string))
{
    T
        | where EventAtCreateRequestStop_ResponseCode != 307
        | where TimeStamp < ago(1h)
        | where 
            ResponseOutcome startswith "Infrastructure:" or 
            ResponseOutcome startswith "Bug:" or 
            CreateOutcome startswith "Infrastructure:" or 
            CreateOutcome startswith "Bug:"
        | make-series Count_ = count() on TimeStamp step 1h
        | mv-expand Count_, TimeStamp
        | project Time = todatetime(TimeStamp), Count=toint(Count_)
}
