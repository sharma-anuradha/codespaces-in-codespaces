.create-or-alter function
with (docstring = 'Computes total reliability rate of create per day.', folder='Dashboards/Reliability/Create')
getCreateReliabilityRate_PerDay
(T:(EventAtCreateRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, CreateOutcome: string))
{
    T
        | where EventAtCreateRequestStop_ResponseCode != 307
        | where TimeStamp < ago(1h)
        | make-series 
            Total_ = count(), 
            Failed_ =  countif(ResponseOutcome startswith "Infrastructure:" or 
                ResponseOutcome startswith "Bug:" or 
                CreateOutcome startswith "Infrastructure:" or 
                CreateOutcome startswith "Bug:") 
            on TimeStamp step 1d
        | mv-expand Total_, Failed_, TimeStamp
        | project Total = toint(Total_), Failed = toint(Failed_), Time = todatetime(TimeStamp)
        | extend Success = Total - Failed
        | extend  Rate = (Success * 100.0) / Total
        | project Time, Rate
}
