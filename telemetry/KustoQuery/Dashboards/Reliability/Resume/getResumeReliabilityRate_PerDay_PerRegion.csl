.create-or-alter function
with (docstring = 'Computes reliability rate of resume per day per region.', folder='Dashboards/Reliability/Resume')
getResumeReliabilityRate_PerDay_PerRegion
(T:(EventAtResumeRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, ResumeOutcome: string, ServiceLocation: string))
{
    T
    | where EventAtResumeRequestStop_ResponseCode != 307
    | where TimeStamp < ago(1h)
    | make-series 
            Total_ = count(), 
            Failed_ =  countif(ResponseOutcome startswith "Infrastructure:" or 
                ResponseOutcome startswith "Bug:" or 
                ResumeOutcome startswith "Infrastructure:" or 
                ResumeOutcome startswith "Bug:") 
            on TimeStamp step 1d by ServiceLocation
    | mv-expand Total_, Failed_, TimeStamp
    | project Total = toint(Total_), Failed = toint(Failed_), Time = todatetime(TimeStamp), ServiceLocation
    | extend Success = Total - Failed
    | extend  Rate = (Success * 100.0) / Total
    | project Time, Rate, ServiceLocation
}
