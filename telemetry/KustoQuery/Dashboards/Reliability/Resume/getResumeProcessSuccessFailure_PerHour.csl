.create-or-alter function
with (docstring = 'Tabulates success vs. failure for resume process per hour', folder='Dashboards/Reliability/Resume')
getResumeProcessSuccessFailure_PerHour
(T:(EventAtResumeRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, ResumeOutcome: string))
{
    T
    | where TimeStamp < ago(1h)
    | where EventAtResumeRequestStop_ResponseCode == 200
    | make-series 
        Available_ = countif(ResumeOutcome == "Available"),
        Failed_ = countif(ResumeOutcome startswith "Infrastructure:" or ResumeOutcome startswith "Bug:") 
        on TimeStamp step 1h
    | mv-expand Available_, Failed_, TimeStamp
    | project 
        Time = todatetime(TimeStamp),
        Success = toint(Available_),
        Failure = toint(Failed_)
}
