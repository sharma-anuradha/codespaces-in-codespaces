.create-or-alter function
with (docstring = 'Computes total reliability rate of shutdown per day.', folder='Dashboards/Reliability/Shutdown')
getShutdownReliabilityRate_PerDay
(T:(EventAtShutdownRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, ShutdownOutcome: string))
{
    T
    | where EventAtShutdownRequestStop_ResponseCode != 307
    | where TimeStamp < ago(1h)
    | make-series 
        Total_ = count(), 
        Failed_ =  countif(ResponseOutcome startswith "Infrastructure:" or 
                ResponseOutcome startswith "Bug:" or 
                ShutdownOutcome startswith "Infrastructure:" or 
                ShutdownOutcome startswith "Bug:")
            on TimeStamp step 1d
    | mv-expand Total_, Failed_, TimeStamp
    | project Total = toint(Total_), Failed = toint(Failed_), Time = todatetime(TimeStamp)
    | extend Success = Total - Failed
    | extend  Rate = (Success * 100.0) / Total
    | project Time, Rate
}
