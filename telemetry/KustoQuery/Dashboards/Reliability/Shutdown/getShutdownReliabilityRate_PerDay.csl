.create-or-alter function
with (docstring = 'Computes total reliability rate of shutdown per day.', folder='Dashboards/Reliability/Shutdown')
getShutdownReliabilityRate_PerDay
(T:(EventAtShutdownRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, ShutdownOutcome: string))
{
    T
    | where EventAtShutdownRequestStop_ResponseCode != 307
    | where TimeStamp < ago(1h)
    | summarize 
        Total = count(),
        Failed =  countif(ResponseOutcome startswith "Infrastructure:" or 
                            ResponseOutcome startswith "Bug:" or 
                            ShutdownOutcome startswith "Infrastructure:" or 
                            ShutdownOutcome startswith "Bug:")
            by bin(TimeStamp, 1d)
    | extend Success = Total - Failed
    | extend  Rate = (Success * 100.0) / Total
    | project Rate, TimeStamp
}
