.create-or-alter function
with (docstring = 'Counts the total infrastructure failures or bugs for shutdown per day.', folder='Dashboards/Reliability/Shutdown')
getShutdownInfrastructureFailures_PerDay
(T:(TimeStamp: datetime, ResponseOutcome: string, ShutdownOutcome: string))
{
    T
    | where TimeStamp  < ago(1h)
    | where ResponseOutcome startswith "Infrastructure:" or 
            ResponseOutcome startswith "Bug:" or 
            ShutdownOutcome startswith "Infrastructure:" or 
            ShutdownOutcome startswith "Bug:"
    | make-series Count_ = count() on TimeStamp step 1d
    | mv-expand Count_, TimeStamp
    | project Time = todatetime(TimeStamp), Count=toint(Count_)
}
