.create-or-alter function
with (docstring = 'Tabulate creation time durations from request start to enviornment available.', folder='Dashboards/Performance/Create')
getCreateRequest_ResponseTime_PerHour
(T:(EventAtCreateRequestStop_ResponseCode: int, TimeStamp: datetime, ResponseOutcome: string, CreateOutcome: string, EventAtCreateRequestStart_Time: datetime, EventAtCreateRequestStop_Time: datetime))
{
    T
    | where TimeStamp < ago(1h)
    | where EventAtCreateRequestStop_ResponseCode != 307
    | where CreateOutcome == "Available" and ResponseOutcome == "Success"
    | extend DurationInSeconds = (EventAtCreateRequestStop_Time - EventAtCreateRequestStart_Time) / 1s
    | make-series percentile(DurationInSeconds, 50), percentile(DurationInSeconds, 90), percentile(DurationInSeconds, 99) on TimeStamp step 1h
    | mv-expand percentile_DurationInSeconds_50, percentile_DurationInSeconds_90, percentile_DurationInSeconds_99, TimeStamp
    | project 
        Time = todatetime(TimeStamp),
        Percentile50 = todouble(percentile_DurationInSeconds_50), 
        Percentile90 = todouble(percentile_DurationInSeconds_90), 
        Percentile99 = todouble(percentile_DurationInSeconds_99)
}
